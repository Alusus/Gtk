اشمل "مـتم/نـص.أسس"؛
اشمل "مـحا.أسس"؛
 مـحا.اشمل_ملف("Alusus/Gtk", "جـتك.أسس")؛
اشمل "بـناء.أسس"؛
اشمل "مـتم/طـرفية.أسس"؛

وحدة دلـيل_الودجات {
    استخدم جـتك؛
    عرف النافذة: سند[نـافذة_تطبيق]؛

    @تصدير[main]
    دالة ابدأ {
        عرف معرف_التطبيق: "org.alusus.gtk-widget.guide"؛
        عرف التطبيق: سند[تـطبيق](تـطبيق.أنشئ(معرف_التطبيق، 0).{
            اربط_إشارة(إشـارة._تفعيل_، جـلب.رد[فعّل]، 0، 0، 0)؛
            شغل(0، 0)؛
        })؛
        جـلب.حرر(التطبيق)؛
    }؛

    دالة فعّل (تطبيق: سند[تـطبيق]، بيانات: مؤشر) {
        // أنشئ النافذة الرئيسية.
        النافذة~عطل_التتبع = نـافذة_تطبيق.أنشئ(تطبيق).{
            عين_الحجم_المبدئي(640، 480)؛
            عين_عنوانا("الأسس: دليل ودجات جتك")؛
            عين_شريط_عنوان(أنشئ_شريط_الحاشية())؛

            أضف(جـانبان.أنشئ(اتـجاه._أفقي_).{
                عين_الموقع(150)؛

                عرف لصيقة: سند[لـصيقة](لـصيقة.أنشئ("السلام عليكم"))؛

                // املأ الجانب القريب.
                احشد1(قـائمة.أنشئ().{
                    عرف س: صحيح؛
                    لكل س = 1، س <= 10، ++س {
                        احشر(لـصيقة.أنشئ(مـتم.نـص("السطر ") + س)، -1)؛
                    }؛
                    اربط_إشارة(إشـارة._اختير_السطر_، جـلب.رد[قد_تغير_اختيار_القائمة]، لصيقة~مؤشر، 0، 0)؛
                }، 0، 1)؛

                // املأ الجانب البعيد.
                احشد2(صـندوق.أنشئ(اتـجاه._عمودي_، 10).{
                    احشد_في_البداية(صـندوق.أنشئ(اتـجاه._أفقي_، 20).{
                        احشد_في_البداية(لصيقة، 1، 0، 0)؛
                        احشد_في_النهاية(صـورة.أنشئ("logo.gif")، 1، 0، 0)؛
                    }، 1، 0، 0)؛
                    
                    احشد_في_البداية(حـقل.أنشئ().{
                        اربط_إشارة(إشـارة._تغير_، جـلب.رد[قد_تغير_الحقل]، لصيقة~مؤشر، 0، 0)؛
                    }، 1، 0، 0)؛

                    عرف صوان_نصي: سند[صـوان_نصي](صـوان_نصي.أنشئ(0))؛

                    احشد_في_البداية(مـساحة_نصية.أنشئ(صوان_نصي).{
                        عين_نمط_اللف(نـمط_اللف._كلمة_)؛
                    }، 1، 1، 0)؛

                    احشد_في_البداية(صـندوق_أزرار.أنشئ(اتـجاه._أفقي_).{
                        عين_النسق(طـراز_صندوق_الأزرار._انتشار_)؛
                        أضف(الـزر.أنشئ_بلصيقة("قُلها").{
                            اربط_إشارة(إشـارة._نقر_، جـلب.رد[قد_نقر_زر_الحوارية]، صوان_نصي~مؤشر، 0، 0)؛
                        })؛

                        أضف(الـزر.أنشئ_بلصيقة("أمحُ").{
                            اربط_إشارة(إشـارة._نقر_، جـلب.رد[قد_نقر_زر_المحو]، صوان_نصي~مؤشر، 0، 0)؛
                        })؛
                    }، 1، 0، 0)؛

                    احشد_في_البداية(الـزر_الثنائي.أنشئ("بدّل").{
                        اربط_إشارة(إشـارة._بدل_، جـلب.رد[قد_بدل_الزر_الثنائي]، لصيقة~مؤشر، 0، 0)؛
                    }، 1، 0، 0)؛

                    احشد_في_البداية(خـانة_تأشير.أنشئ("أشّر").{
                        اربط_إشارة(إشـارة._بدل_، جـلب.رد[قد_بدل_الزر_الثنائي]، لصيقة~مؤشر، 0، 0)؛
                    }، 1، 0، 0)؛
                }، 1، 1)؛
            })؛
            
            أظهر_الكل()؛
        }؛
    }؛

    دالة أنشئ_شريط_الحاشية () => سند[شـريط_الحاشية] {
        أرجع شـريط_الحاشية.أنشئ().{
            عين_عنوانا("الأسس: دليل ودجات جتك")؛

            احشد_في_البداية(الـزر.أنشئ_بلصيقة("حول").{
                اربط_إشارة(إشـارة._نقر_، جـلب.رد[قد_نقر_زر_حول]، 0، 0، 0)؛
            })؛

            احشد_في_النهاية(الـزر.أنشئ_بلصيقة("خروج").{
                اربط_إشارة(إشـارة._نقر_، جـلب.رد[قد_نقر_زر_الخروج]، 0، 0، 0)؛
            })؛

            احشد_في_النهاية(مـطلق_القائمة.أنشئ().{
                عين_منبثق(أنشئ_قائمة_الأوامر())؛
            })؛
        }؛
    }

    دالة أنشئ_قائمة_الأوامر (): سند[قـائمة_أوامر] {
        أرجع قـائمة_أوامر.أنشئ().{
            ألحق(عـنصر_قائمة_أوامر.أنشئ("حول").{
                اربط_إشارة(إشـارة._تفعيل_، جـلب.رد[قد_نقر_زر_حول]، 0، 0، 0)؛
                أظهر()؛
            })؛
            ألحق(عـنصر_قائمة_أوامر.أنشئ("خروج").{
                اربط_إشارة(إشـارة._تفعيل_، جـلب.رد[قد_نقر_زر_الخروج]، 0، 0، 0)؛
                أظهر()؛
            })؛
        }؛
    }

    دالة قد_تغير_اختيار_القائمة (قائمة: سند[قـائمة]، عنصر: سند[سـلة]، لصيقة: سند[لـصيقة]) {
        عرف محارف: مصفوفة[محرف، 100]؛
        مـتم.نـص.عين(
            محارف~مؤشر،
            "العنصر المختار: %s"،
            لـصيقة.هات_النص(سـلة.هات_ابن(عنصر)~مؤشر~مثل[مؤشر[لـصيقة]]~محتوى)
        )؛
        لصيقة.عين_النص(محارف~مؤشر)؛
    }؛

    دالة قد_تغير_الحقل (قابل_للتعديل: سند[قـابل_للتعديل]، لصيقة: سند[لـصيقة]) {
        عرف محارف: مصفوفة[محرف، 100]؛
        مـتم.نـص.عين(محارف~مؤشر، "النص المدخل: %s"، قـابل_للتعديل.هات_محارف(قابل_للتعديل، 0، -1))؛
        لصيقة.عين_النص(محارف~مؤشر)؛
    }؛

    دالة قد_نقر_زر_حول (ودجة: سند[ودجـة]، معطيات: مؤشر) {
        عرف حوارية: سند[حـوارية_رسالة](حـوارية_رسالة.أنشئ(
            النافذة، 0، صـنف_رسالة._معلومة_، نـوع_الزر._أغلق_، "الأسس: دليل ودجات جتك.\جبرمجة: سرمد خالد عبد الله"
        ))؛
        حوارية.شغل()؛
        ودجـة.مزق(حوارية)؛
    }؛

    دالة قد_نقر_زر_الحوارية (ودجة: سند[ودجـة]، صوان_نصي: سند[صـوان_نصي]) {
        // هات النص من الصوان.
        عرف نص: مؤشر[مصفوفة[محرف]]؛
        عرف مكرر_البداية: مـكرر_نصي؛
        عرف مكرر_النهاية: مـكرر_نصي؛
        صوان_نصي.هات_مكرر_البداية(مكرر_البداية)؛
        صوان_نصي.هات_مكرر_النهاية(مكرر_النهاية)؛
        نص = صوان_نصي.هات_النص(مكرر_البداية، مكرر_النهاية، 1)؛

        عرف حوارية: سند[حـوارية_رسالة](حـوارية_رسالة.أنشئ(
            النافذة، 0، صـنف_رسالة._معلومة_، نـوع_الزر._أغلق_، نص
        ))؛
        حوارية.شغل()؛
        ودجـة.مزق(حوارية)؛
    }؛

    دالة قد_نقر_زر_المحو (ودجة: سند[ودجـة]، صوان_نصي: سند[صـوان_نصي]) {
        صوان_نصي.عين_النص(""، 0)؛
    }؛

    دالة قد_بدل_الزر_الثنائي (زر: سند[الـزر_الثنائي]، لصيقة: سند[لـصيقة]) {
        عرف فعال: ثنائي؛
        فعال = زر.أهو_فعال()؛
        إذا فعال لصيقة.عين_النص("الزر الآن مضغوط")
        وإلا لصيقة.عين_النص("الزر الآن غير مضغوط")؛
    }؛

    دالة قد_نقر_زر_الخروج (ودجة: سند[ودجـة]، معطيات: مؤشر) {
        ودجـة.مزق(النافذة)؛
    }؛
}؛

إذا الـعملية.عدد_المعطيات < 3 {
      دلـيل_الودجات.ابدأ()؛
} وإلا {
    عرف تنفيذي: بـناء.تـنفيذي(دلـيل_الودجات.ابدأ~شبم، الـعملية.المعطيات~محتوى(الـعملية.عدد_المعطيات - 1))؛
    تنفيذي.أضف_اعتمادية(جـتك~شبم)؛
    إذا تنفيذي.أنتج() {
        مـتم.طـرفية.اطبع("اكتمل البناء.\ج")؛
    } وإلا {
        مـتم.طـرفية.اطبع("فشل البناء.\ج")؛
    }؛
}؛

