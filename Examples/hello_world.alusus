import "Apm.alusus";
Apm.importFile("Alusus/Gtk");
import "Build.alusus";

def HelloWorld: module
{
    use Gtk;
    def window: ref[AppWindow];

    @expname[main]
    func start {
        def app: ref[App](App.new("org.alusus.gtk-hello-world", 0).{
            connectSignal(Signal.ACTIVATE, Glib.cb[activate], 0, 0, 0);
        });
        def status: Int = app.exec(0, 0);
        Glib.unref(app);
    };

    func activate (app: ref[App], data: ptr): Void {
        window~no_deref = AppWindow.new(app).{
            setTitle("Alusus GTK Hello World");
            setDefaultSize(480, 320);
            add(Box.new(Orientation.VERTICAL, 20).{
                packStart(Label.new("Hello World!"), 1, 0, 0);
                packStart(ButtonBox.new(Orientation.HORIZONTAL).{
                    setLayout(ButtonBoxStyle.SPREAD);
                    add(Button.newWithLabel("About").{
                        connectSignal(Signal.CLICKED, Glib.cb[onAboutClicked], 0, 0, 0);
                    });
                    add(Button.newWithLabel("Quit").{
                        connectSignal(Signal.CLICKED, Glib.cb[onQuitClicked], 0, 0, 0);
                    });
                }, 1, 0, 0);
            });
            showAll();
        }
    };

    func onAboutClicked (widget: ref[Widget], data: ptr): Void {
        def dialog: ref[MessageDialog](MessageDialog.new(
            window, 0, MessageType.INFO, ButtonsType.CLOSE,
            "Alusus GTK Hello World.\nauthor: Sarmad Abdullah"
        ));
        dialog.exec();
        Widget.destroy(dialog);
    };

    func onQuitClicked (widget: ref[Widget], data: ptr): Void {
        Widget.destroy(window);
    };
};

if Process.argCount < 3 {
    HelloWorld.start();
} else {
    def exe:Build.Exe(HelloWorld.start~ast, Process.args~cnt(Process.argCount - 1));
    exe.addDependency(Gtk~ast);
    if exe.generate() {
        Srl.Console.print("Build complete.\n");
    } else {
        Srl.Console.print("Build failed.\n");
    };
};
