اشمل "مـحا.أسس"؛
مـحا.اشمل_ملف("Alusus/Gtk", "جـتك.أسس")؛
اشمل "بـناء.أسس"؛
اشمل "مـتم/طـرفية.أسس"؛

استخدم جـتك؛

وحدة الـبسملة {
    عرف النافذة: سند[نـافذة_تطبيق]؛

    @تصدير[main]
    دالة ابدأ {
        عرف معرف_التطبيق: "org.alusus.gtk-basmalah"؛
        عرف التطبيق: سند[تـطبيق](تـطبيق.أنشئ(معرف_التطبيق، 0).{
            اربط_إشارة(إشـارة._تفعيل_، فعّل~مؤشر~مثل[مؤشر[دالة]]، 0، 0، 0)؛
        })؛
        عرف الحالة: صحيح = التطبيق.شغل(0، 0)؛
        جـلب.حرر(التطبيق)؛
    }؛

    دالة فعّل (تطبيق: سند[تـطبيق]، معلومات: مؤشر) {
        النافذة~عطل_التتبع = نـافذة_تطبيق.أنشئ(تطبيق).{
            عين_عنوانا("الأسس/وحدة جـتك: البسملة")؛
            عين_الحجم_المبدئي(480، 320)؛
            
            أضف(صـندوق.أنشئ(اتـجاه._عمودي_، 20).{
                احشد_في_البداية(لـصيقة.أنشئ("بسم الله الرحمن الرحيم")، 1، 0، 0)؛
                
                احشد_في_البداية(صـندوق_أزرار.أنشئ(اتـجاه._أفقي_).{
                    عين_النسق(طـراز_صندوق_الأزرار._انتشار_)؛

                    أضف(الـزر.أنشئ_بلصيقة("حول").{
                        اربط_إشارة(إشـارة._نقر_، جـلب.رد[قد_نقر_زر_حول]، 0، 0، 0)؛
                    })؛

                    أضف(الـزر.أنشئ_بلصيقة("خروج").{
                        اربط_إشارة(إشـارة._نقر_، جـلب.رد[قد_نقر_زر_الخروج]، 0، 0، 0)؛
                    })؛
                }، 1، 0، 0)؛
            })؛
            
            أظهر_الكل()؛
        }؛
    }؛

    دالة قد_نقر_زر_حول (عنصر: سند[ودجـة]، معلومات: مؤشر) {
        عرف حوارية: سند[حـوارية](حـوارية_رسالة.أنشئ(
           النافذة، 0، صـنف_رسالة._معلومة_، نـوع_الزر._أغلق_، "البسملة.\nتطوير: سرمد خالد عبد الله"
        ))؛
        حوارية.شغل()؛
        ودجـة.مزق(حوارية)؛
    }؛

    دالة قد_نقر_زر_الخروج (عنصر: سند[ودجـة]، معلومات: مؤشر) {
        ودجـة.مزق(النافذة)؛
    }؛
}؛

إذا الـعملية.عدد_المعطيات < 3 {
      الـبسملة.ابدأ()؛
} وإلا {
    عرف تنفيذي: بـناء.تـنفيذي(الـبسملة.ابدأ~شبم، الـعملية.المعطيات~محتوى(الـعملية.عدد_المعطيات - 1))؛
    تنفيذي.أضف_اعتمادية(جـتك~شبم)؛
    إذا تنفيذي.أنتج() {
        مـتم.طـرفية.اطبع("اكتمل البناء.\ج")؛
    } وإلا {
        مـتم.طـرفية.اطبع("فشل البناء.\ج")؛
    }؛
}؛

